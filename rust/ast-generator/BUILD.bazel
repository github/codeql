load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")
load("//misc/bazel:rust.bzl", "codeql_rust_binary")

# Get the ra_ap_syntax repo for rust.ungram
# This is provided by the vendor_rust__ra_ap_syntax repo
alias(
    name = "rust.ungram",
    actual = "@vendor_rust__ra_ap_syntax-0.0.301//:rust.ungram",
    visibility = ["//rust/codegen:__pkg__"],
)

_codegen = [
    "grammar.rs",
    "grammar/ast_src.rs",
]

_codegen_srcs = ["@rust-analyzer-src//:xtask/src/codegen/%s" % f for f in _codegen]

_codegen_outs = ["src/codegen/%s" % f for f in _codegen]

genrule(
    name = "codegen",
    srcs = _codegen_srcs,
    outs = _codegen_outs,
    cmd = "\n".join(
        ["mkdir -p $(RULEDIR)/src/codegen/grammar"] +
        [
            "cp $(location %s) $(RULEDIR)/%s" % item
            for item in zip(_codegen_srcs, _codegen_outs)
        ],
    ),
)

codeql_rust_binary(
    name = "ast-generator",
    srcs = glob(
        ["src/**/*.rs"],
        exclude = ["src/codegen/**"],
    ) + [":codegen"],
    aliases = {
        "//misc/bazel/3rdparty/rust_deps:stdx": "stdx",
    },
    args = ["$(rlocationpath :rust.ungram)"],
    data = [":rust.ungram"] + glob(["templates/*.mustache"]),
    visibility = ["//rust:__subpackages__"],
    deps = [
        "//misc/bazel/3rdparty/rust_deps:anyhow",
        "//misc/bazel/3rdparty/rust_deps:either",
        "//misc/bazel/3rdparty/rust_deps:itertools",
        "//misc/bazel/3rdparty/rust_deps:mustache",
        "//misc/bazel/3rdparty/rust_deps:proc-macro2",
        "//misc/bazel/3rdparty/rust_deps:quote",
        "//misc/bazel/3rdparty/rust_deps:serde",
        "//misc/bazel/3rdparty/rust_deps:stdx",
        "//misc/bazel/3rdparty/rust_deps:ungrammar",
    ],
)

write_file(
    name = "update",
    out = "update.sh",
    content = [
        "#!/bin/bash",
        ". misc/bazel/runfiles.sh",
        'DST_DIR="$(dirname "$(rlocation "$1")")"',
        'mkdir -p "$DST_DIR/src/codegen/grammar"',
    ] + [
        # using cat instead of cp to honor default umask
        # (also, macOS does not support `cp --no-preserve=mode`)
        'cat "$(rlocation "$%s")" > "$DST_DIR/%s"' % item
        for item in enumerate(
            ["rust.ungram"] + _codegen_outs,
            2,
        )
    ],
    is_executable = True,
)

sh_binary(
    name = "inject-sources",
    srcs = [":update"],
    args = ["$(rlocationpath %s)" % f for f in [
        "Cargo.toml",
        ":rust.ungram",
    ] + _codegen_outs],
    data = [
        "Cargo.toml",
        ":rust.ungram",
    ] + _codegen_outs,
    deps = ["//misc/bazel:sh_runfiles"],
)

exports_files(["Cargo.toml"])
