import '../lib.just'

install: (_bazel "run" "@codeql//swift:install")

[group('build')]
build: (_build_dist "swift")

generate: (_bazel "run" "@codeql//swift/codegen")

@_check_clang_format:
    if ! which clang-format > /dev/null; then \
        "{{ JUST_EXECUTABLE }}" _run_in_semmle_code "tools/bazel" "run" "//c/clang-format:install"; \
    fi

format ARGS=".": _check_clang_format (_run "clang-format" "-i" ("$(find " + ARGS + " -type f -name '*.h' -or -name '*.cpp')"))
import "../../ql/swift/ql/justfile"

[group('test')]
language-tests *EXTRA_ARGS: (_language_tests EXTRA_ARGS source_dir() 'ql/test')

[group('test')]
extra-tests: (_sembuild "target/test/check-queries-swift") (_sembuild "target/test/check-db-upgrades-swift") (_sembuild "target/test/check-db-downgrades-swift")
